: SQ DUP * ;
: CR 10 EMIT ;

: MARK    DP 0 , ;
: RESOLVE DP OVER - SWAP ! ;
: IF      ['] JZ  C, MARK ; IMMEDIATE
: ELSE    ['] JMP C, MARK SWAP RESOLVE ; IMMEDIATE
: THEN    RESOLVE ; IMMEDIATE
: BEGIN   DP ; IMMEDIATE
: UNTIL   ['] JZ  C, DP - , ; IMMEDIATE
: AGAIN   ['] JMP C, DP - , ; IMMEDIATE
: WHILE   ['] JZ C, MARK ; IMMEDIATE
: REPEAT  SWAP ['] JMP C, DP - , RESOLVE ; IMMEDIATE

: VARIABLE
  CREATE
  ['] LIT  C, DP 3 + ,
  ['] EXIT C,
  DP 2 + DP!
; IMMEDIATE 

: CONSTANT
  DP 2 - @ >R
  DP 3 - DP!
  CREATE
  ['] LIT  C, R> ,
  ['] EXIT C,
; IMMEDIATE 

: ( BEGIN KEY 41 = UNTIL ; IMMEDIATE

: CASE 0 ; IMMEDIATE
: OF
  ['] OVER C, ['] = C,
  ['] JZ   C, MARK
  ['] DROP C,
; IMMEDIATE

: ENDOF
    SWAP 1 + SWAP
    ['] JMP C, MARK SWAP
    RESOLVE
    SWAP
; IMMEDIATE

: ENDCASE
  BEGIN
    DUP 0 >
  WHILE
    1 - SWAP RESOLVE
  REPEAT
  DROP
; IMMEDIATE

: 2DUP OVER OVER ;
: 2DROP DROP DROP ;
: FACTORIAL
  1 2 ROT
  BEGIN
      2DUP <=
  WHILE
      -ROT TUCK
      * SWAP
      1 + ROT
  REPEAT
  2DROP ;

: CHOOSE
  CASE
    1 OF 10 ENDOF
    2 OF 20 ENDOF
    3 OF 30 ENDOF
    DROP 99
  ENDCASE ;

: NON-ZERO? C@ 0 <> ;

: TYPE
  BEGIN
    DUP NON-ZERO?
  WHILE
    DUP C@ EMIT
    1 +
  REPEAT
  DROP ;

: s"
  ['] LIT C, DP 5 + , ( addr of string )
  ['] JMP C, MARK
  BEGIN
    KEY DUP 34 <>
  WHILE
    C,
  REPEAT
  DROP
  0 C,
  RESOLVE
; IMMEDIATE

VARIABLE V1
VARIABLE V2

2 CONSTANT TWO

ENTRY

3 V1 !
5 V2 !

2 3 < IF
  V1 @ SQ 1 + . CR
  V2 @ FACTORIAL . CR
ELSE
  ( shouldn't  be executed )
  65 EMIT
THEN

TWO CHOOSE . CR
3   CHOOSE . CR
7   CHOOSE . CR

s" Hello world!" TYPE CR

DEPTH . CR

BYE

: KEY  2 IN  ;
: RND  3 IN ;
: EMIT 1 OUT ;
: C!
  DUP @ $FF00 AND
  ROT $FF AND OR
  SWAP ! ;

: C@  @ $FF AND ;
: ++  DUP @ 1+ SWAP ! ;
: CODE R> DUP C@ SWAP 1+ >R ;
: SUB  R> 1+ DUP @ SWAP 2 + >R ;

: DP $182 ;

: ,   DP @  ! DP ++ DP ++ ;
: C,  DP @ C! DP ++ ;

: CELL 2 ;
: CELLS CELL * ;
: ALLOT DP @ + DP ! ;
: EXEC >R EXIT ;
: DEPTH SP $02 - CELL / ;

: MARK    DP @ 0 , ;
: RESOLVE DP @ OVER - SWAP ! ;
: IF      CODE JZ  C, MARK ; IMMEDIATE
: ELSE    CODE JMP C, MARK SWAP RESOLVE ; IMMEDIATE
: THEN    RESOLVE ; IMMEDIATE
: BEGIN   DP @ ; IMMEDIATE
: UNTIL   CODE JZ  C, DP @ - , ; IMMEDIATE
: AGAIN   CODE JMP C, DP @ - , ; IMMEDIATE
: WHILE   CODE JZ C, MARK ; IMMEDIATE
: REPEAT  SWAP CODE JMP C, DP @ - , RESOLVE ; IMMEDIATE
: FOR     CODE >R C, DP @ ; IMMEDIATE
: NEXT
  CODE R@ C, CODE JZ  C, MARK
  CODE R> C, CODE 1-  C, CODE >R C,
  SWAP
  CODE JMP C, DP @ - ,
  RESOLVE
  CODE R> C, CODE DROP C,
; IMMEDIATE

: VARIABLE
  MAKE-WORD
  CODE LIT  C, MARK
  CODE EXIT C,
  DP @ SWAP !
; IMMEDIATE

: ( BEGIN KEY 41 = UNTIL ; IMMEDIATE
: \ BEGIN KEY DUP 10 = SWAP 13 = OR UNTIL ; IMMEDIATE

: CASE 0 ; IMMEDIATE
: OF
  CODE OVER C, CODE = C,
  CODE JZ   C, MARK
  CODE DROP C,
; IMMEDIATE

: ENDOF
    SWAP 1+ SWAP
    CODE JMP C, MARK SWAP
    RESOLVE
    SWAP
; IMMEDIATE

: ENDCASE
  BEGIN
    DUP 0 >
  WHILE
    1- SWAP RESOLVE
  REPEAT
  DROP
; IMMEDIATE

: NON-ZERO? C@ 0 <> ;

: PRINT
  BEGIN
    DUP NON-ZERO?
  WHILE
    DUP C@ EMIT
    1+
  REPEAT
  DROP ;

: "
  CODE LIT C, MARK ( Mark1: addr of string )
  CODE JMP C, MARK ( Mark2: length )
  SWAP
  DP @ SWAP !     ( Resolve Mark1 )
  BEGIN
    KEY DUP 34 <>
  WHILE
    C,
  REPEAT
  DROP
  0 C,
  RESOLVE         ( Resolve Mark2 )
; IMMEDIATE

: ?DUP  DUP 0 <> IF DUP THEN ;
: 2DUP  OVER OVER ;
: 2DROP DROP DROP ;
: 3DROP DROP DROP DROP ;
: -ROT  ROT ROT ;
: TUCK  DUP ROT ROT ;

: BETWEEN? OVER >= -ROT <= AND ;

: CR 10 EMIT ;

: /MOD 2DUP % -ROT / ;

: . ( n -- )
    DUP 0 < IF 45 EMIT -1 * THEN
    10 /MOD ?DUP IF . THEN
    48 + EMIT ;

( ************************************* )
( Layout:                               )
( 0x0164: USER CODE DEFAULT ENTRY       )
( ...                                   )
( 0x3000: USER HEAP START               )
( ...                                   )
( 0x7000: COMPILER CODE                 )
( ************************************* )
CONSTANT TIB      0x64
CONSTANT F_IMME   0b10000000
CONSTANT TRUE    -1
CONSTANT FALSE    0
CONSTANT START-IP 0x0164
CONSTANT USR-HEAP 0x3000

VARIABLE POS
VARIABLE LAST
VARIABLE STEPPER

0 LAST !

: 2DROP DROP DROP ;
: 3DROP DROP DROP DROP ;
: 2DUP  OVER OVER ;
: ?DUP  DUP 0 <> IF DUP THEN ;

: CR 10 EMIT ;

: SPACE?
  DUP  9  =
  OVER 32 = OR
  OVER 10 = OR
  SWAP 13 = OR ;

: NON-SPACE? SPACE? INVERT ;

: POS++ POS @ 1 + POS ! ;
: STORE POS @ C! POS++ ;
: RESET TIB POS ! ;

: CHR= C@ SWAP C@ = ;
: NON-ZERO? C@ 0 <> ;

: STRING=
  BEGIN
    2DUP CHR= 
    OVER NON-ZERO? AND
    OVER NON-ZERO? AND
  WHILE
    1 + SWAP 1 +
  REPEAT
  CHR= ;

: LENGTH
  0
  BEGIN
    OVER NON-ZERO?
  WHILE
    1 + SWAP
    1 + SWAP
  REPEAT
  NIP ;

: TYPE
  BEGIN
    DUP NON-ZERO?
  WHILE
    DUP C@ EMIT
    1 +
  REPEAT
  DROP ;

: SKIP BEGIN KEY DUP SPACE? WHILE DROP REPEAT STORE ;

: WORD
  RESET SKIP
  BEGIN
    KEY DUP NON-SPACE?
  WHILE
    STORE
  REPEAT
  DROP
  0 STORE
  TIB ;

: STRING,
  BEGIN
    DUP NON-ZERO?
  WHILE
    DUP C@ C,
    1 +
  REPEAT
  C@ C, ;

: >NFA 2 + ;
: >FFA >NFA DUP LENGTH + 1 + ;
: >CFA >FFA 1 + ;

: STEP  STEPPER @ @ STEPPER ! ;
: STEP? STEPPER @ 0 <> ;
: NFA   STEPPER @ >NFA ;

: IMMEDIATE? >FFA C@ F_IMME AND 0 <> ;

: IMMEDIATE
  LAST @ >FFA C@
  F_IMME OR 
  LAST @ >FFA C!

: FIND ( s -- addr / 0 )
  LAST @ STEPPER !
  BEGIN
    STEP?
  WHILE
    NFA OVER ( s ) STRING= IF
      DROP
      STEPPER @
      EXIT
    THEN
    STEP
  REPEAT
  DROP 0 ;

: >DIGIT    C@ 48 - ;
: NONDIGIT? C@ DUP 48 < SWAP 57 > OR ;
: MINUS?    C@ 45 = ;

: >NUMBER ( s -- n bool )
  DUP MINUS? IF 1 + -1 ELSE 1 THEN
  SWAP 0
  ( sign str result )
  BEGIN
    OVER NON-ZERO?
  WHILE
    OVER NONDIGIT? IF 3DROP FALSE EXIT THEN
    10 * OVER >DIGIT +
    SWAP 1 + SWAP
  REPEAT
  NIP * TRUE ;

: CONVERT ( -- n bool ) TIB >NUMBER ;

: >OPCODE ( s -- opcode / 0 )
  DUP s" +"        STRING= IF DROP 0x01 EXIT THEN
  DUP s" -"        STRING= IF DROP 0x02 EXIT THEN
  DUP s" *"        STRING= IF DROP 0x03 EXIT THEN
  DUP s" /"        STRING= IF DROP 0x04 EXIT THEN
  DUP s" DUP"      STRING= IF DROP 0x05 EXIT THEN
  DUP s" DROP"     STRING= IF DROP 0x06 EXIT THEN
  DUP s" SWAP"     STRING= IF DROP 0x07 EXIT THEN
  DUP s" NIP"      STRING= IF DROP 0x08 EXIT THEN
  DUP s" OVER"     STRING= IF DROP 0x09 EXIT THEN
  DUP s" ROT"      STRING= IF DROP 0x0A EXIT THEN
  DUP s" -ROT"     STRING= IF DROP 0x0B EXIT THEN
  DUP s" TUCK"     STRING= IF DROP 0x0C EXIT THEN
  DUP s" INVERT"   STRING= IF DROP 0x0D EXIT THEN
  DUP s" AND"      STRING= IF DROP 0x0E EXIT THEN
  DUP s" OR"       STRING= IF DROP 0x0F EXIT THEN
  DUP s" XOR"      STRING= IF DROP 0x10 EXIT THEN
  DUP s" >"        STRING= IF DROP 0x11 EXIT THEN
  DUP s" >="       STRING= IF DROP 0x12 EXIT THEN
  DUP s" <"        STRING= IF DROP 0x13 EXIT THEN
  DUP s" <="       STRING= IF DROP 0x14 EXIT THEN
  DUP s" ="        STRING= IF DROP 0x15 EXIT THEN
  DUP s" <>"       STRING= IF DROP 0x16 EXIT THEN
  DUP s" JMP"      STRING= IF DROP 0x17 EXIT THEN
  DUP s" JZ"       STRING= IF DROP 0x18 EXIT THEN
  DUP s" JNZ"      STRING= IF DROP 0x19 EXIT THEN
  DUP s" AJMP"     STRING= IF DROP 0x1A EXIT THEN
  DUP s" CALL"     STRING= IF DROP 0x1B EXIT THEN
  DUP s" EXIT"     STRING= IF DROP 0x1C EXIT THEN
  DUP s" ."        STRING= IF DROP 0x1D EXIT THEN
  DUP s" NOP"      STRING= IF DROP 0x1E EXIT THEN
  DUP s" EMIT"     STRING= IF DROP 0x1F EXIT THEN
  DUP s" LIT"      STRING= IF DROP 0x20 EXIT THEN
  DUP s" %"        STRING= IF DROP 0x21 EXIT THEN
  DUP s" KEY"      STRING= IF DROP 0x22 EXIT THEN
  DUP s" SP"       STRING= IF DROP 0x23 EXIT THEN
  DUP s" SP!"      STRING= IF DROP 0x24 EXIT THEN
  DUP s" HALT"     STRING= IF DROP 0x25 EXIT THEN
  DUP s" LSHIFT"   STRING= IF DROP 0x26 EXIT THEN
  DUP s" RSHIFT"   STRING= IF DROP 0x27 EXIT THEN
  DUP s" !"        STRING= IF DROP 0x28 EXIT THEN
  DUP s" C!"       STRING= IF DROP 0x29 EXIT THEN
  DUP s" @"        STRING= IF DROP 0x2A EXIT THEN
  DUP s" C@"       STRING= IF DROP 0x2B EXIT THEN
  DUP s" DP"       STRING= IF DROP 0x2C EXIT THEN
  DUP s" DP!"      STRING= IF DROP 0x2D EXIT THEN
  DUP s" ,"        STRING= IF DROP 0x2E EXIT THEN
  DUP s" C,"       STRING= IF DROP 0x2F EXIT THEN
  DUP s" DEPTH"    STRING= IF DROP 0x30 EXIT THEN
  DUP s" >R"       STRING= IF DROP 0x31 EXIT THEN
  DUP s" R>"       STRING= IF DROP 0x32 EXIT THEN
  DUP s" EXEC"     STRING= IF DROP 0x33 EXIT THEN
  DUP s" DUMP"     STRING= IF DROP 0x34 EXIT THEN
  DUP s" ABORT"    STRING= IF DROP 0x35 EXIT THEN
  DUP s" [']"      STRING= IF DROP 0x36 EXIT THEN
  DROP 0
;

: FIND-PRIMITIVE ( -- opcode / 0 ) TIB >OPCODE ;

: ??? ( s -- ) TIB TYPE 32 EMIT 63 EMIT CR ;

: COMPILE ( -- )
  TIB FIND
  ?DUP IF
    DUP IMMEDIATE? IF
      >CFA EXEC
    ELSE
      OPCODE: CALL C,
      >CFA , 
    THEN
  ELSE
    FIND-PRIMITIVE
    ?DUP IF
      ( OPCODE ) C,
    ELSE CONVERT
      IF
        OPCODE: LIT C, ( NUM ) ,
      ELSE ??? THEN
    THEN
  THEN ;

: MAKE-HEADER ( name -- )
  DP
  LAST @ ,
  LAST !
  STRING, 
  0 ( FLAGS ) C, ;

: CREATE WORD MAKE-HEADER ;

: END OPCODE: EXIT C, ;

: COMPILE-AJMP OPCODE: AJMP C, START-IP , ;
: COMPILE-HALT OPCODE: LIT  C, 0 , OPCODE: HALT C, ;

: COMPILER-LOOP
  1 DP!
  COMPILE-AJMP
  START-IP DP!
  BEGIN
    WORD s" BYE" STRING= INVERT
  WHILE
    COMPILE
  REPEAT
  COMPILE-HALT ;

: DUMP-OUTPUT
  s" output.img" DUMP
  INVERT IF
    s" DUMP FAILED" TYPE CR
    ABORT
  THEN ;

: POST-CHECKS
  DEPTH 0 <> IF
    s" Non Empty stack: " TYPE DEPTH . CR
    ABORT
  THEN ;

ENTRY

( ***************** Dictionary Structure ***************** )
( Words:                                                   )
(  16b            8b 8b                                    )
(  LINK "<name1>" 00 FLAG INSTR.1 .. INSTR.N EXIT LINK ... )
(   ^---------------------------------------------+        )

USR-HEAP DP! ( <= User Dictionary Start )

s" :" MAKE-HEADER
  OPCODE: CALL C, ' CREATE  ,
END IMMEDIATE

s" ;" MAKE-HEADER
  OPCODE: LIT C, 0x1C , ( EXIT )
  OPCODE: C, C,
END IMMEDIATE

s" IMMEDIATE" MAKE-HEADER
  OPCODE: CALL C, ' IMMEDIATE ,
END IMMEDIATE

s" CREATE" MAKE-HEADER
  OPCODE: CALL C, ' CREATE ,
END

s" ENTRY" MAKE-HEADER
  OPCODE: DP  C, 
  OPCODE: LIT C, 0x0002 , 
  OPCODE: !   C,
END IMMEDIATE

COMPILER-LOOP
DUMP-OUTPUT
POST-CHECKS

0 HALT
